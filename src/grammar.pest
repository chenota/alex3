// Ignore whitespace
WHITESPACE = _{ " " | "\t" | "\n" | "\r" }

// Entry point
query = {
    insert_query | select_query
}

// Insert data query
insert_query = {
    insert_data_keyword ~ "{" ~ (triple ~ ".")* ~ "}"
}

// SELECT query
select_query = {
    select_keyword ~ variable+ ~ "WHERE" ~ "{" ~ (triple ~ ".")* ~ "}"
}

// Triple definitions
triple = {
    subject ~ predicate ~ object
}
subject = { iri | blank_node | variable }
predicate = { iri | "a" }
object = { iri | string_literal | blank_node | variable }

// Terminals
variable = @{ "?" ~ name }
blank_node = @{ "_:" ~ name }

iri = @{ "<" ~ (!">" ~ ANY)* ~ ">" }
name = @{ (ASCII_ALPHANUMERIC | "_" | "-")+ }
string_literal = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

// Keywords
insert_data_keyword = _{ "INSERT" ~ "DATA" | "insert" ~ "data" }
select_keyword = _{ "SELECT" | "select" }